plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'jacoco'
	id 'com.github.kt3k.coveralls' version '2.12.2' // ì´ í”ŒëŸ¬ê·¸ì¸ ì»¤ë²„ë¦¬ì§€ ì½˜ì†”ì— ì¶œë ¥ ê°€ëŠ¥
}

group = 'io.dustin'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'mysql:mysql-connector-java:8.0.32'
	implementation 'jakarta.persistence:jakarta.persistence-api:3.1.0'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.kafka:spring-kafka'
	implementation 'com.fasterxml.jackson.core:jackson-databind'


	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.mockito:mockito-core:5.11.0'
	testImplementation 'com.h2database:h2'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

// âœ… JaCoCo ì„¤ì • ì¶”ê°€
jacoco {
	toolVersion = "0.8.10"
}

jacocoTestReport {
	dependsOn test
	// ./gradlew clean test jacocoTestReport <- í…ŒìŠ¤íŠ¸ë¥¼ ì´ˆê¸°í™”í•˜ê³  ìƒˆë¡œ í…ŒìŠ¤íŠ¸ë¥¼ ì§„í–‰ í›„ ë¦¬í¬íŠ¸ë¥¼ ì‘ì„±í•¨
	reports {
		html.required.set(true)     // HTML ë³´ê³ ì„œ
		xml.required = true
		csv.required.set(true)     // CSV ë³´ê³ ì„œ í•„ìš”ì‹œ true
	}
	doLast {
		def rawXml = new File("${buildDir}/reports/jacoco/test/jacocoTestReport.xml").text

		// DOCTYPE ì œê±°
		rawXml = rawXml.replaceAll(/<!DOCTYPE[^>]+>/, "")

		def parser = new XmlSlurper()
		def report = parser.parseText(rawXml)

		def totalMissed = 0
		def totalCovered = 0

		report.counter.each {
			totalMissed += it.@missed.toInteger()
			totalCovered += it.@covered.toInteger()
		}

		def total = totalMissed + totalCovered
		def percentage = total > 0 ? (100 * totalCovered / total).round(2) : 0

		println "\nğŸ§ª ì´ í…ŒìŠ¤íŠ¸ ì»¤ë²„ë¦¬ì§€: ${percentage}% (ì»¤ë²„ëœ ì¤„ ìˆ˜: ${totalCovered}, ì „ì²´: ${total})"
	}


}


jacocoTestCoverageVerification {
	violationRules {
		rule {
			enabled = true
//			element = 'CLASS'
			element = 'BUNDLE'


			limit {
				counter = 'LINE'
 value = 'COVEREDRATIO'
				minimum = 0.80 //
			}
		}
	}
}

check.dependsOn jacocoTestCoverageVerification
